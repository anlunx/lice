group 'org.lice'
version '3.3.2'

buildscript {
	ext.kotlin_version = '1.2.41'
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
	}
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
}

sourceSets {
	main.kotlin.srcDirs = ['src/main/kotlin']
	main.java.srcDirs = []
	main.resources.srcDirs = ['src/main/resources']
	test.kotlin.srcDirs = ['src/test/kotlin', 'src/test/java']
	test.java.srcDirs = ['src/test/java']
}

configurations {
	library
}

dependencies {
	compile group: 'org.jetbrains', name: 'annotations', version: '16.0.1'
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

	configurations.compile.extendsFrom(configurations.library)
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

bintray {
	user = "ice1000"
	key = findProperty("key").toString()
	configurations = ["archives"]
	pkg {
		name = project.name
		repo = "Lice"
		githubRepo = "lice-lang/lice"
		publicDownloadNumbers = true
		vcsUrl = "https://github.com/lice-lang/lice.git"
		version {
			name = project.version
			vcsTag = "v$project.version"
			websiteUrl = "https://github.com/lice-lang/lice/releases/tag/$vcsTag"
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication.class) {
			from components.java
			groupId = group
			artifactId = project.name
			version = project.version
			artifact sourcesJar
			pom.withXml {
				def root = asNode()
				root.appendNode("description", "A multi-paradigm programming language running on JVM")
				root.appendNode("name", project.name)
				root.children().last()
			}
		}
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

mainClassName = 'org.lice.repl.Main'

jar {
	manifest {
		attributes 'Main-Class': mainClassName
		attributes 'Implementation-Version': version
		attributes 'Implementation-Title': "Lice v$version"
	}
}

task fatJar(type: Jar) {
	classifier = 'all'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	manifest {
		attributes 'Main-Class': 'org.lice.repl.Main'
		attributes 'Implementation-Version': version
		attributes 'Implementation-Title': "Lice v$version"
	}
	with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	description 'Assembles a jar archive containing the source code of this project.'
	group 'build'
	from sourceSets.main.allSource
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled false
	}
}

check.dependsOn jacocoTestReport

artifacts {
	archives jar
	archives fatJar
	archives sourcesJar
}
